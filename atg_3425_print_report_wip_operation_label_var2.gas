Program.Sub.ScreenSU.Start
gui.SS_Form1..create
gui.SS_Form1..caption("WIP Opertion Label Var2")
gui.SS_Form1..size(7105,5990)
gui.SS_Form1..position(0,0)
gui.SS_Form1..event(unload,unload)
gui.SS_Form1..alwaysontop(False)
gui.SS_Form1..fontname("Arial")
gui.SS_Form1..fontsize(8)
gui.SS_Form1..forecolor(0)
gui.SS_Form1..fontstyle(,,,,)
gui.SS_Form1..BackColor(-2147483633)
gui.SS_Form1..controlbox(True)
gui.SS_Form1..maxbutton(False)
gui.SS_Form1..minbutton(False)
gui.SS_Form1..mousepointer(0)
gui.SS_Form1..moveable(True)
gui.SS_Form1..sizeable(False)
gui.SS_Form1..ShowInTaskBar(True)
gui.SS_Form1..titlebar(True)
gui.SS_Form1.lbl_txtLblQty.create(label,"# of Labels",True,1800,200,1,100,135,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.txtLblQty.create(textbox,"",True,1800,330,0,100,335,True,0,Arial,8,16777215,1)
gui.SS_Form1.lbl_txtSeq.create(label,"Sequence",True,1800,200,1,100,835,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.txtSeq.create(textbox,"",True,1530,330,0,100,1035,True,0,Arial,8,16777215,1)
gui.SS_Form1.lbl_txtWC.create(label,"Workcenter",True,1800,200,1,1715,835,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.txtWC.create(textbox,"",True,2070,330,0,1670,1035,True,0,Arial,8,16777215,1)
gui.SS_Form1.lbl_txtDesc.create(label,"Description",True,1800,200,1,3780,835,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.txtDesc.create(textbox,"",True,3000,330,0,3765,1035,True,0,Arial,8,16777215,1)
gui.SS_Form1.lbl_lvw1.create(label,"Click a line to select a sequence",True,2895,200,1,100,1535,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.lvw1.create(listview)
gui.SS_Form1.lvw1.view(3)
gui.SS_Form1.lvw1.addlistviewcolumn("Sequence",1500,0)
gui.SS_Form1.lvw1.addlistviewcolumn("Workcenter",2118,0)
gui.SS_Form1.lvw1.addlistviewcolumn("Description",3000,0)
gui.SS_Form1.lvw1.visible(True)
gui.SS_Form1.lvw1.size(6755,2000)
gui.SS_Form1.lvw1.zorder(0)
gui.SS_Form1.lvw1.position(100,1735)
gui.SS_Form1.lvw1.enabled(True)
gui.SS_Form1.lvw1.fontname("Arial")
gui.SS_Form1.lvw1.fontsize(8)
gui.SS_Form1.lvw1.event(itemclick,lvw1_itemclick)
gui.SS_Form1.cmd_lvw1Add.create(button)
gui.SS_Form1.cmd_lvw1Add.caption("Add")
gui.SS_Form1.cmd_lvw1Add.visible(True)
gui.SS_Form1.cmd_lvw1Add.size(1000,330)
gui.SS_Form1.cmd_lvw1Add.zorder(0)
gui.SS_Form1.cmd_lvw1Add.position(5755,1035)
gui.SS_Form1.cmd_lvw1Add.enabled(True)
gui.SS_Form1.cmd_lvw1Add.fontname("Arial")
gui.SS_Form1.cmd_lvw1Add.fontsize(8)
gui.SS_Form1.cmd_lvw1Add.SetInternationalID(7600)
gui.SS_Form1.cmd_lvw1Delete.create(button)
gui.SS_Form1.cmd_lvw1Delete.caption("Delete")
gui.SS_Form1.cmd_lvw1Delete.visible(True)
gui.SS_Form1.cmd_lvw1Delete.size(1000,330)
gui.SS_Form1.cmd_lvw1Delete.zorder(0)
gui.SS_Form1.cmd_lvw1Delete.position(100,3820)
gui.SS_Form1.cmd_lvw1Delete.enabled(True)
gui.SS_Form1.cmd_lvw1Delete.fontname("Arial")
gui.SS_Form1.cmd_lvw1Delete.fontsize(8)
gui.SS_Form1.cmd_lvw1Delete.event(click,cmdprnt_click)
gui.SS_Form1.cmd_lvw1Delete.SetInternationalID(171500)
gui.SS_Form1.cmd_lvw1Clear.create(button)
gui.SS_Form1.cmd_lvw1Clear.caption("Clear")
gui.SS_Form1.cmd_lvw1Clear.visible(True)
gui.SS_Form1.cmd_lvw1Clear.size(1000,330)
gui.SS_Form1.cmd_lvw1Clear.zorder(0)
gui.SS_Form1.cmd_lvw1Clear.position(1185,3820)
gui.SS_Form1.cmd_lvw1Clear.enabled(True)
gui.SS_Form1.cmd_lvw1Clear.fontname("Arial")
gui.SS_Form1.cmd_lvw1Clear.fontsize(8)
gui.SS_Form1.cmd_lvw1Clear.SetInternationalID(106700)



Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Program.Sub.Preflight.End


Program.Sub.Main.Start


V.Local.ssel.Declare(String)
V.Local.sJobSuff.Declare(String)

F.ODBC.Connection!Con.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

'==============================================
'if called from populate hook, just change the label on the button
F.Intrinsic.Control.If(V.Caller.Hook,=,"15840")
	Variable.passed.000048.set("Move Ticket")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

'if not called from any of the correct hooks, end
'script1 button on GUI update job
F.Intrinsic.Control.If(V.Caller.Hook,<>,"15850")
'Process button on update job
F.Intrinsic.Control.andIf(V.Caller.Hook,<>,"15890")
'Process button on update multiple jobs
F.Intrinsic.Control.andIf(V.Caller.Hook,<>,"15915")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

'verify trmnl is on ATG_Move_Tckt_Setup to be able to print label/comments.  If not an authorzed terminal, end
F.Intrinsic.String.Concat("select * from ATG_MOVE_TCKT_SETUP where TRMNL='",V.Caller.Terminal,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstX",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstX.EOF,=,True)
		F.Intrinsic.Control.CallSub(Unload)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstX.Close

'verify there are Qty to print
F.Intrinsic.Control.If(V.Passed.000001,=,".000000")
F.Intrinsic.Control.AndIf(V.Passed.000002,=,".000000")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

'==============================================
'forms designer sets up a lot of standard features that aren't needed for this project, so disabling them
Gui.SS_Form1.txtDesc.Enabled(false)
Gui.SS_Form1.txtSeq.Enabled(false)
Gui.SS_Form1.txtWC.Enabled(false)
Gui.SS_Form1.cmd_lvw1Add.Enabled(false)
Gui.SS_Form1.cmd_lvw1Add.Visible(false)
Gui.SS_Form1.cmd_lvw1Clear.Enabled(false)
Gui.SS_Form1.cmd_lvw1Clear.Visible(false)
'can use the one button, but need it to say "Print", instead of "Delete"
Gui.SS_Form1.cmd_lvw1Delete.Caption("Print")
'load the grid, before the screen opens
F.Intrinsic.Control.CallSub(Loadgrid)
'default label qty to 1, justto have a qty
Gui.SS_Form1.txtLblQty.Text("1")
'write job-suffix to label
F.Intrinsic.String.Concat("Job: ",V.Passed.009990," - ",V.Passed.009991," - ",V.Passed.009992,V.Local.sJobSuff)
gui.SS_Form1.lbl6.create(label,V.Local.sJobSuff,True,2760,255,1,3875,335,True,1,Arial,8,-2147483633,0)
'show screen
Gui.SS_Form1..Show

Program.Sub.Main.End


Program.Sub.Unload.Start

F.ODBC.Connection!Con.Close
F.Intrinsic.Control.End

Program.Sub.Unload.End


program.sub.cmdprnt_click.start

Gui.SS_Form1..Visible(false)

V.Local.sjob.Declare(String)
V.Local.ssuffix.Declare(String)
V.Local.sseq.Declare(String)
V.Local.sQty.Declare(String)
V.Local.sQtyCompleted.Declare(String)
V.Local.sWC.Declare(String)
V.Local.sEmployee.Declare(String)
V.Local.sscrapqty.Declare(String)
V.Local.sCloseflag.Declare(String)
V.Local.sScrapCode.Declare(String)
V.Local.sScrapDescription.Declare(String)
V.Local.sLMO.Declare(String)
V.Local.sQualitynbr.Declare(String)
V.Local.sCurPart.Declare(String)
V.Local.sCurDescr.Declare(String)
V.Local.sPart.Declare(String)
V.Local.sPartDescr.Declare(String)
V.Local.sCust.Declare(String)
V.Local.sName.Declare(String)
V.Local.sNextSeq.Declare(String)
V.Local.sNextPart.Declare(String)
V.Local.sNextDescr.Declare(String)
V.Local.sWOQty.Declare(String)
V.Local.sWOCustQty.Declare(String)
V.Local.sWOPriority.Declare(String)
V.Local.sWODateDue.Declare(Date)
V.Local.sNext_DateStart.Declare(Date)
V.Local.sNext_DateDue.Declare(Date)
V.Local.sTopParentPart.Declare(String)
V.Local.sTopParentJob.Declare(String)
V.Local.sTopParentSuffix.Declare(String)
V.Local.sTopParentDescr.Declare(String)
V.Local.sParentPart.Declare(String)
V.Local.sParentDescr.Declare(String)
V.Local.sParentJob.Declare(String)
V.Local.sParentSuffix.Declare(String)
V.Local.sSel.Declare(String)
V.Local.sSelA.Declare(String)
V.Local.iFor.Declare(Long)

'clear values from ATG_3425 table
F.Intrinsic.String.Concat("delete from ATG_3425_WIP_Oper_Label where TRMNL='",V.Caller.Terminal,"'",V.Local.sSel)
F.ODBC.Connection!Con.Execute(V.Local.sSel)

V.Local.sjob.Set(V.passed.009990)
V.Local.ssuffix.Set(V.Passed.009991)
V.Local.sseq.Set(V.Passed.009992)
V.Local.sQty.Set(V.Passed.000001)
V.Local.sWC.Set(V.Passed.000010)
V.Local.sEmployee.Set(V.Passed.009994)
V.Local.sscrapqty.Set(V.Passed.000002)
V.Local.sCloseflag.Set(V.Passed.000003)
V.Local.sQualitynbr.Set(V.Passed.010002)

'if good and scrap pieces is 0 then don't continue
F.Intrinsic.Control.If(V.Local.sQty,=,".000000")
F.Intrinsic.Control.AndIf(V.Local.sscrapqty,=,".000000")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

'next seq selected on the screen
V.Local.sNextSeq.Set(V.Screen.SS_Form1!txtSeq.Text)

'currnt seq
F.Intrinsic.String.Concat("select PART,DESCRIPTION,UNITS_COMPLETE from V_JOB_OPERATIONS where JOB='",V.Local.sjob,"' and SUFFIX='",V.Local.ssuffix,"' and SEQ='",V.Local.sseq,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,False)
		V.Local.sCurPart.Set(V.ODBC.Con!rstSel.FieldValTrim!PART)
		V.Local.sCurDescr.Set(V.ODBC.Con!rstSel.FieldValTrim!DESCRIPTION)
		V.Local.sQtyCompleted.Set(V.ODBC.Con!rstSel.FieldValTrim!UNITS_COMPLETE)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSel.Close

'"next" seq
F.Intrinsic.String.Concat("select SEQ,PART,DESCRIPTION,LMO,DATE_START,DATE_DUE,UNITS_COMPLETE from V_JOB_OPERATIONS where JOB='",V.Local.sjob,"' and SUFFIX='",V.Local.ssuffix,"' and SEQ='",V.Local.sNextSeq,"' and LMO <> 'C'  and LMO <> 'M' order by SEQ",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,False)
		V.Local.sNextPart.Set(V.ODBC.Con!rstSel.FieldValTrim!PART)
		V.Local.sNextDescr.Set(V.ODBC.Con!rstSel.FieldValTrim!DESCRIPTION)
		V.Local.sLMO.Set(V.ODBC.Con!rstSel.FieldValTrim!LMO)
		V.Local.sNext_DateStart.Set(V.ODBC.Con!rstSel.FieldValTrim!DATE_START)
		V.Local.sNext_DateDue.Set(V.ODBC.Con!rstSel.FieldValTrim!DATE_DUE)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSel.Close

'header information
F.Intrinsic.String.Concat("select PART,CUSTOMER,PRIORITY,PART_DESCRIPTION,QTY_ORDER,QTY_CUSTOMER,DATE_DUE, Parent_WO,Parent_Suffix_Parent  from V_JOB_HEADER where JOB='",V.Local.sjob,"' and SUFFIX='",V.Local.ssuffix,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,False)
		V.Local.sParentJob.Set(V.ODBC.Con!rstSel.FieldValTrim!Parent_WO)
		V.Local.sParentSuffix.Set(V.ODBC.Con!rstSel.FieldValTrim!Parent_Suffix_Parent)
		V.Local.sPart.Set(V.ODBC.Con!rstSel.FieldValTrim!PART)
		V.Local.sCust.Set(V.ODBC.Con!rstSel.FieldValTrim!CUSTOMER)
		V.Local.sWOPriority.Set(V.ODBC.Con!rstSel.FieldValTrim!PRIORITY)
		V.Local.sPartDescr.Set(V.ODBC.Con!rstSel.FieldValTrim!PART_DESCRIPTION)
		V.Local.sWOQty.Set(V.ODBC.Con!rstSel.FieldValTrim!QTY_ORDER)
		V.Local.sWOCustQty.Set(V.ODBC.Con!rstSel.FieldValTrim!QTY_CUSTOMER)
		V.Local.sWODateDue.Set(V.ODBC.Con!rstSel.FieldValTrim!DATE_DUE)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSel.Close

'Parent Part information
F.Intrinsic.String.Concat("select Part, Part_Description from V_JOB_HEADER where Job='",V.Local.sParentJob,"' and Suffix='",V.Local.sParentSuffix,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSelA",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSelA.EOF,=,False)
		V.Local.sParentPart.Set(V.ODBC.Con!rstSelA.FieldValTrim!Part)
		V.Local.sParentDescr.Set(V.ODBC.Con!rstSelA.FieldValTrim!Part_Description)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSelA.Close

'Top Parent (BOM)
F.Intrinsic.String.Concat("select Job,Suffix,Part, Part_Description from V_JOB_HEADER where Job='",V.Local.sjob,"' and SUFFIX='000'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,False)
		V.Local.sTopParentJob.Set(V.ODBC.Con!rstSel.FieldValTrim!Job)
		V.Local.sTopParentSuffix.Set(V.ODBC.Con!rstSel.FieldValTrim!Suffix)
		V.Local.sTopParentPart.Set(V.ODBC.Con!rstSel.FieldValTrim!Part)
		V.Local.sTopParentDescr.Set(V.ODBC.Con!rstSel.FieldValTrim!Part_Description)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSel.Close

'customer name
F.Intrinsic.String.Concat("select NAME_CUSTOMER from V_CUSTOMER_MASTER where CUSTOMER='",V.Local.sCust,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,False)
		V.Local.sName.Set(V.ODBC.Con!rstSel.FieldValTrim!NAME_CUSTOMER)
	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSel.Close

'scrap reason
F.Intrinsic.String.Concat("select SCRAP_REASON from V_JOBS_IN_PROCESS where JOB='",V.Local.sjob,"' and SUFFIX='",V.Local.ssuffix,"' and SEQUENCE ='",V.Local.sseq,"' and EMPLOYEE_NAME='",V.Local.sEmployee,"'  order by sequence_key desc",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRO("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,False)

		V.Local.sScrapCode.Set(V.ODBC.Con!rstSel.FieldValTrim!SCRAP_REASON)
		F.Intrinsic.String.Concat("select SCRAP_CODE_DESC from V_RJCT_SCRP_CODES where SCRAP_CODE='",V.Local.sScrapCode,"'",V.Local.sSelA)
		F.ODBC.Connection!Con.OpenRecordsetRO("rstSelA",V.Local.sSelA)
			F.Intrinsic.Control.If(V.ODBC.Con!rstSelA.EOF,=,False)
			V.Local.sScrapDescription.Set(V.ODBC.Con!rstSelA.FieldValTrim!SCRAP_CODE_DESC)
			F.Intrinsic.Control.EndIf
		F.ODBC.Con!rstSelA.Close

	F.Intrinsic.Control.EndIf
F.ODBC.Con!rstSel.Close

F.Intrinsic.String.Concat("select * from ATG_3425_WIP_Oper_Label where TRMNL='",V.Caller.Terminal,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRW("rstSel",V.Local.sSel)
'need to have a record on the table for each label.
F.Intrinsic.Control.for(V.Local.iFor,1,V.Screen.SS_Form1!txtLblQty.Text,1)
	F.ODBC.Con!rstSel.AddNew
		F.ODBC.Con!rstSel.Set!TRMNL(V.Caller.Terminal)
		F.ODBC.Con!rstSel.Set!JOB(V.Local.sjob)
		F.ODBC.Con!rstSel.Set!SUFFIX(V.Local.ssuffix)
		F.ODBC.Con!rstSel.Set!CUR_SEQ(V.Local.sseq)
		F.ODBC.Con!rstSel.Set!CUR_PART(V.Local.sCurPart)
		F.ODBC.Con!rstSel.Set!CUR_DESCR(V.Local.sCurDescr)
		F.ODBC.Con!rstSel.Set!LMO(V.Local.sLMO)
		F.ODBC.Con!rstSel.Set!QTY(V.Local.sQty)
		F.ODBC.Con!rstSel.Set!WORKCENTER(V.Local.sWC)
		F.ODBC.Con!rstSel.Set!EMPLOYEE(V.Local.sEmployee)
		F.ODBC.Con!rstSel.Set!QUALITY_NO(V.Local.sQualitynbr)
		F.ODBC.Con!rstSel.Set!SCRAP_CODE(V.Local.sScrapCode)
		F.ODBC.Con!rstSel.Set!SCRAP_CODE_DESC(V.Local.sScrapDescription)
		F.ODBC.Con!rstSel.Set!SCRAP_QTY(V.Local.sscrapqty)
		F.ODBC.Con!rstSel.Set!CLOSE_FLAG(V.Local.sCloseflag)
		F.ODBC.Con!rstSel.Set!PART(V.Local.sPart)
		F.ODBC.Con!rstSel.Set!CUSTOMER(V.Local.sCust)
		F.ODBC.Con!rstSel.Set!NAME(V.Local.sName)
		F.ODBC.Con!rstSel.Set!NEXT_SEQ(V.Local.sNextSeq)
		F.ODBC.Con!rstSel.Set!NEXT_PART(V.Local.sNextPart)
		F.ODBC.Con!rstSel.Set!NEXT_DESCR(V.Local.sNextDescr)
		F.ODBC.Con!rstSel.Set!Parent_Job(V.Local.sParentJob)
		F.ODBC.Con!rstSel.Set!Parent_Suffix(V.Local.sParentSuffix)
		F.ODBC.Con!rstSel.Set!Parent_Part(V.Local.sParentPart)
		F.ODBC.Con!rstSel.Set!Parent_Descr(V.Local.sParentDescr)
		F.ODBC.Con!rstSel.Set!Top_Parent_Job(V.Local.sTopParentJob)
		F.ODBC.Con!rstSel.Set!Top_Parent_Suffix(V.Local.sTopParentSuffix)
		F.ODBC.Con!rstSel.Set!Top_Parent_Part(V.Local.sTopParentPart)
		F.ODBC.Con!rstSel.Set!Top_Parent_Descr(V.Local.sTopParentDescr)
		F.ODBC.Con!rstSel.Set!QUALITY_NO(V.Local.sQualitynbr)
		F.ODBC.Con!rstSel.Set!NEXT_SEQ_START(V.Local.sNext_DateStart)
		F.ODBC.Con!rstSel.Set!NEXT_SEQ_DUE(V.Local.sNext_DateDue)
		F.ODBC.Con!rstSel.Set!WO_PRIORITY(V.Local.sWOPriority)
		F.ODBC.Con!rstSel.Set!PART_DESCR(V.Local.sPartDescr)
		F.ODBC.Con!rstSel.Set!WO_QTY(V.Local.sWOQty)
		F.ODBC.Con!rstSel.Set!WO_CUSTOMER_QTY(V.Local.sWOCustQty)
		F.ODBC.Con!rstSel.Set!WO_DUE_DATE(V.Local.sWODateDue)
		F.ODBC.Con!rstSel.Set!QTY_COMPLETED(V.Local.sQtyCompleted)
	F.ODBC.Con!rstSel.Update
F.Intrinsic.Control.Next(V.Local.iFor)
F.ODBC.Con!rstSel.Close

F.Intrinsic.Control.CallSub(Printlabel,"Job",V.Local.sjob,"Suffix",V.Local.ssuffix,"Qty",V.Local.sQty,"ScrapQty",V.Local.sscrapqty)


program.sub.cmdprnt_click.end


Program.Sub.PrintLabel.Start

V.Local.sRptLoc.Declare(String)
V.Local.iRptId.Declare(Long)
V.Local.sQty.Declare(String)
V.Local.sScrapFlag.Declare(String)
V.Local.sParams.Declare(String)
V.Local.sValues.Declare(String)
V.Local.sScrapQty.Declare(String)
V.Local.iFor.Declare(Long)
V.Local.sLblQty.Declare(String)


F.Intrinsic.String.Concat(V.Caller.BusintDir,"\Custom\WIP_Operation_Label_Var2.rpt",V.Local.srptLoc)
F.Global.BI.InitializeReport("WIP_Operation_Label_Var2",V.Local.srptLoc,V.Local.iRptId)

'F.Intrinsic.Control.for(V.Local.iFor,1,V.Screen.SS_Form1!txtLblQty.Text,1)
'	F.Intrinsic.String.Concat(V.Local.iFor," of ",V.Screen.SS_Form1!txtLblQty.Text,V.Local.sLblQty)
	F.Intrinsic.Control.If(V.Args.Qty,<>,".000000")
		V.Local.sScrapflag.Set("N")
		F.Intrinsic.String.Split("TRMNL*!*Job*!*Suffix*!*Scrapflag*!*LblQty","*!*",V.Local.sParams)
		F.Intrinsic.String.Concat(V.Caller.Terminal,"*!*",V.Args.job,"*!*",V.Args.suffix,"*!*",V.Local.sScrapflag,"*!*",V.Screen.SS_Form1!txtLblQty.Text,V.Local.sValues)
		F.Intrinsic.String.Split(V.Local.sValues,"*!*",V.Local.sValues)
		F.Global.BI.PrintReport(V.Local.iRptId,0,V.Local.sParams,V.Local.sValues,"",False)
	F.Intrinsic.Control.Endif

	F.Intrinsic.Control.If(V.Args.scrapqty,<>,".000000")
		V.Local.sScrapflag.Set("Y")
		F.Intrinsic.String.Split("TRMNL*!*Job*!*Suffix*!*Scrapflag*!*LblQty","*!*",V.Local.sParams)
		F.Intrinsic.String.Concat(V.Caller.Terminal,"*!*",V.Args.job,"*!*",V.Args.suffix,"*!*",V.Local.sScrapflag,"*!*",V.Screen.SS_Form1!txtLblQty.Text,V.Local.sValues)
		F.Intrinsic.String.Split(V.Local.sValues,"*!*",V.Local.sValues)
		F.Global.BI.PrintReport(V.Local.iRptId,0,V.Local.sParams,V.Local.sValues,"",False)
	F.Intrinsic.Control.Endif
'F.Intrinsic.Control.Next(V.Local.iFor)

F.Intrinsic.Control.CallSub(Unload)

Program.Sub.PrintLabel.End


program.sub.lvw1_itemclick.start



V.Local.sRet.Declare(String)
V.Local.sOrd.Declare(String)
V.Local.iOrd.Declare(Long)

'get the item key
Gui.SS_Form1.lvw1.RetrieveSelectedListItems(V.Local.sOrd)
'get the item ordinal
Gui.SS_Form1.lvw1.RetrieveItemOrdinal(V.Local.sOrd,V.Local.iOrd)

'get the line of text
V.Local.sRet.Set(V.Screen.SS_Form1!lvw1.ListItemTextExtended(V.Local.iOrd))
'split to get trmnl and comment
	F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
'write values to text boxes
	Gui.SS_Form1.txtSeq.Text(V.Local.sRet(0).Trim)
	Gui.SS_Form1.txtWC.Text(V.Local.sRet(1).Trim)
	Gui.SS_Form1.txtDesc.Text(V.Local.sRet(2).Trim)

program.sub.lvw1_itemclick.end


Program.Sub.LoadGrid.Start


V.Local.sSel.Declare(String)
V.Local.sPrct.Declare(String)
V.Local.fPrct.Declare(Float)
V.Local.sTotal.declare(String)
V.Local.bNew.Declare(Boolean)

'job = V.passed.009990
'suffix = V.Passed.009991
'seq V.Passed.009992

'look for sequences and load list view
F.Intrinsic.String.Concat("select Seq, Part, Description from Job_Operations where (LMO='L' or LMO='O') and Job='",V.passed.009990,"'and Suffix='",V.passed.009991,"' and Seq >'",V.passed.009992,"' and seq <'995000'order by Seq",V.Local.sSel)
V.Local.sPrct.Set(V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRW("rstX",V.Local.sSel)
	F.Intrinsic.Control.DoUntil(V.ODBC.Con!rstX.EOF,=,True)
		Gui.SS_Form1.lvw1.AddListItem(V.ODBC.Con!rstX.FieldValTrim!Seq,V.ODBC.Con!rstX.FieldValTrim!Seq)
		Gui.SS_Form1.lvw1.SetListItemSubItemText(V.ODBC.Con!rstX.FieldValTrim!Seq,1,V.ODBC.Con!rstX.FieldValTrim!Part)
		Gui.SS_Form1.lvw1.SetListItemSubItemText(V.ODBC.Con!rstX.FieldValTrim!Seq,2,V.ODBC.Con!rstX.FieldValTrim!Description)
	F.ODBC.Con!rstX.MoveNext
	F.Intrinsic.Control.Loop
F.ODBC.Con!rstX.Close


Program.Sub.LoadGrid.End


Program.Sub.Comments.Start
${$0$}$ATG_Print_Report_WIP_Operation_Labe$}$JCT$}$6/26/2012
${$1$}$$}$$}$2$}$15915$}$OLS003M-PB-PROCESS-PRE-HOOK (On-line GUI)$}$6/26/2012 8:57:48 AM$}$(Program: OLG002; Screen: OLS003M)

${$1$}$$}$$}$1$}$15890$}$OLS003-PB-PROCESS-POST-HOOK (On-line GUI)$}$6/26/2012 8:56:45 AM$}$(Program: OLG002; Screen: OLS003)

${$1$}$$}$$}$0$}$15850$}$OLS003-PB-SCRIPT-1-HOOK (On-line GUI)$}$6/26/2012 8:56:05 AM$}$(Program: OLG002; Screen: OLS003)

${$2$}$$}$$}$0$}$4$}$WIP_Operation_Label_ATG.rpt$}$6/26/2012 8:59:15 AM$}$Label report
${$3$}$0$}$$}$-1$}$-1$}$$}$1/1/1900$}$Quote Number: 3294
Customer Name: Flynn Canada, Ltd.
Reference Number: FLY010-11620121535-5
Title: Custom ACH File Export (BMO Financial Group)

${$4$}$0$}$$}$0$}$-1$}$JCT$}$8/26/2011 8:54:37 AM$}$modified to accept/create custom BI report ID

${$4$}$0$}$$}$1$}$-1$}$JCT$}$6/26/2012 8:55:24 AM$}$cusomt version for TC Millwork to add ability to specify the "next" sequence to print, and the number of labels
Program.Sub.Comments.End
Program.Sub.ScreenSS.Start
SS_Form1{{CAPTION::WIP Opertion Label Var2
SS_Form1.CTRL{{NAME::txtLblQty\\TYPE::2\\CAPTION::# of Labels\\GROUP::1\\BROWSER::0\\SIZE::1
SS_Form1.CTRL{{NAME::txtSeq\\TYPE::2\\CAPTION::Sequence\\RANGE::lvw1\\GROUP::2\\BROWSER::0\\SIZE::1
SS_Form1.CTRL{{NAME::txtWC\\TYPE::2\\CAPTION::Workcenter\\RANGE::lvw1\\GROUP::2\\BROWSER::0\\SIZE::1
SS_Form1.CTRL{{NAME::txtDesc\\TYPE::2\\CAPTION::Description\\RANGE::lvw1\\GROUP::2\\BROWSER::0\\SIZE::1
SS_Form1.CTRL{{NAME::lvw1\\TYPE::7\\CAPTION::Click a line to select a sequence\\GROUP::3\\BROWSER::0\\SIZE::1

Program.Sub.ScreenSS.End

